## Reference Grammar for PROFETA

Start           ::= Strategy

Strategy        ::= Plan
                  | Plan Strategy

Plan            ::= Head ">>" Body

Head            ::= "(" Event ")"
                  | "(" Event "|" Condition ")"

Body            ::= "[" IntentionList "]"

Event           ::= GoalEvent
                  | BeliefEvent

GoalEvent       ::= "+" Goal
                  | "-" Goal

BeliefEvent     ::= "+" Belief
                  | "-" Belief

Condition       ::= "(" ConditionTerms ")"

ConditionTerms  ::= Belief
                  | Belief "&" ConditionTerms
                  | "(" LambdaExpr ")"

IntentionList   ::= Intention "," IntentionList
                  | Intention
                  | empty 

Intention       ::= Event
                  | AtomicAction

# ----------------------------------------------------------
# Lambda Section
# ----------------------------------------------------------
LambdaExpr      ::= LAMBDA ":" LambdaTest

LambdaTest      ::= Comparison
                  | CompoundTest

CompoundTest    ::= UnaryTest ORLITERAL CompoundTest
                  | UnaryTest ANDLITERAL CompoundTest
                  | UnaryTest

UnaryTest       ::= NOTLITERAL "(" Comparison ")"
                  | "(" Comparison ")"


Comparison      ::= CompTerm CompOp CompTerm
 
CompTerm        ::= NAME
                  | NUMBER
                  | STRING

CompOp          ::= ">"  
                  | "<"
                  | EQUALS
                  | GREATEQ
                  | LESSEQ
                  | NOTEQ
# ----------------------------------------------------------

Belief          ::= NAME "(" ArgumentList ")"

Goal            ::= "~" NAME "(" ArgumentList ")"

AtomicAction    ::= NAME "(" ArgumentList ")"

ArgumentList    ::= Argument "," ArgumentList
                  | Argument
                  | empty

Argument        ::= STRING
                  | USCORE "(" VARIABLE ")"
                  | NUMBER
                  | NAME
                  | "[" ArgumentList "]"
                  | "(" ArgumentList ")"


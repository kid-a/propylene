## Reference Grammar for PROFETA

Strategy        ::= Plan
                  | Strategy Plan

Plan            ::= "(" Head ")" ">>" '[' IntentionList ']'

Head            ::= Event
                  | Event "|" "(" Condition ")"

Event           ::= GoalEvent
                  | BeliefEvent

GoalEvent       ::= "+" Goal
                  | "-" Goal

BeliefEvent     ::= "+" Belief
                  | "-" Belief

Condition       ::= Belief
                  | Belief "&" Condition
                  | "(" LambdaExpr ")"

IntentionList   ::= Intention ',' IntentionList
                  | Intention
                  | empty 

Intention       ::= Event
                  | AtomicAction

# ----------------------------------------------------------
# Lambda Section
# ----------------------------------------------------------
LambdaExpr      ::= LAMBDA ":" LambdaTest

LambdaTest      ::= CompTerm CompOp CompTerm
                  | "(" LambdaTest ")"
                  | "(" LambdaTest ")" ORLITERAL  LambdaTest 
                  | "(" LambdaTest ")" ANDLITERAL  LambdaTest 
 
CompTerm        ::= NAME
                  | NUMBER
                  | STRING

CompOp          ::= ">"  
                  | "<"
                  | EQUALS
                  | GREATEQ
                  | LESSEQ
                  | NOTEQ
# ----------------------------------------------------------

Belief          ::= NAME "(" ArgumentList ")"

Goal            ::= "~" NAME "(" ArgumentList ")"

AtomicAction    ::= NAME "(" ArgumentList ")"

ArgumentList    ::= Argument "," ArgumentList
                  | Argument
                  | empty

Argument        ::= STRING
                  | USCORE "(" VARIABLE ")"
                  | NUMBER
                  | NAME
                  | "[" ArgumentList "]"
                  | "(" ArgumentList ")"


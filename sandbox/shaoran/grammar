## Reference Grammar for PROFETA

Strategy        ::= Plan
                  | Strategy Plan

Plan            ::= "(" Head ")" ">>" Body

Head            ::= Event
                  | Event "|" "(" Condition ")"

Event           ::= GoalEvent
                  | BeliefEvent

GoalEvent       ::= "+" "~" Belief
                  | "-" "~" Belief

BeliefEvent     ::= "+" Belief
                  | "-" Belief

Condition       ::= "(" LambdaExpr ")"
                  | Belief
                  | Belief "&" Condition

Body            ::= "[" "]"
                  | "[" IntentionList "]"

IntentionList   ::= Intention
                  | IntentionList "," Intention

Intention       ::= Event
                  | AtomicAction

# ----------------------------------------------------------
# Lambda Section
# ----------------------------------------------------------
LambdaExpr      ::= LAMBDA ":" LambdaTest
                  | LAMBDA LambdaArgs ":" LambdaTest

LambdaArgs      ::= NAME
                  | LambdaArgs "," NAME

LambdaTest      ::= CompTerm CompOp CompTerm
                  | "(" LambdaTest ")"
                  | "(" LambdaTest ")" ORLITERAL  LambdaTest 
                  | "(" LambdaTest ")" ANDLITERAL  LambdaTest 
 
CompTerm        ::= NAME
                  | NUMBER
                  | STRING

CompOp          ::= ">"  
                  | "<"
                  | EQUALS
                  | GREATEQ
                  | LESSEQ
                  | NOTEQ
# ----------------------------------------------------------

Belief          ::= NAME "(" ")"
                  | NAME "(" ArgumentList ")"

AtomicAction    ::= NAME "(" ")"
                  | NAME "(" ArgumentList ")"

ArgumentList    ::= Argument
                  | ArgumentList "," Argument

Argument        ::= STRING
                  | USCORE "(" STRING ")"
                  | NUMBER


